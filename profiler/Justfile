# Profile with flamegraph using a preset
flame preset method iterations="10000":
    CARGO_PROFILE_BENCH_DEBUG=true cargo flamegraph \
        -o {{preset}}-{{method}}.svg \
        --package jsonschema-profiler \
        -- \
        --preset {{preset}} \
        --method {{method}} \
        --iterations {{iterations}}
    @echo "Opening {{preset}}-{{method}}.svg in browser..."
    @xdg-open {{preset}}-{{method}}.svg 2>/dev/null || open {{preset}}-{{method}}.svg 2>/dev/null || echo "Please open {{preset}}-{{method}}.svg manually"

# Profile with dhat using a preset
dhat preset method iterations="10000":
    cargo run --release --features dhat-heap --package jsonschema-profiler \
        -- \
        --preset {{preset}} \
        --method {{method}} \
        --iterations {{iterations}}

# Profile with custom schema and instance
flame-custom method schema instance iterations="10000":
    CARGO_PROFILE_BENCH_DEBUG=true cargo flamegraph \
        -o custom-{{method}}.svg \
        --package jsonschema-profiler \
        -- \
        --schema {{schema}} \
        --instance {{instance}} \
        --method {{method}} \
        --iterations {{iterations}}
    @echo "Opening custom-{{method}}.svg in browser..."
    @xdg-open custom-{{method}}.svg 2>/dev/null || open custom-{{method}}.svg 2>/dev/null || echo "Please open custom-{{method}}.svg manually"

# Profile with custom schema only (no instance needed for build/registry methods)
flame-schema method schema iterations="10000":
    CARGO_PROFILE_BENCH_DEBUG=true cargo flamegraph \
        -o schema-{{method}}.svg \
        --package jsonschema-profiler \
        -- \
        --schema {{schema}} \
        --method {{method}} \
        --iterations {{iterations}}
    @echo "Opening schema-{{method}}.svg in browser..."
    @xdg-open schema-{{method}}.svg 2>/dev/null || open schema-{{method}}.svg 2>/dev/null || echo "Please open schema-{{method}}.svg manually"

citm-build: (flame "citm" "build" "10000")
citm-is-valid: (flame "citm" "is_valid" "10000")
citm-validate: (flame "citm" "validate" "1000")
citm-iter-errors: (flame "citm" "iter_errors" "1000")
citm-apply: (flame "citm" "apply" "1000")

openapi-build: (flame "openapi" "build" "10000")
openapi-is-valid: (flame "openapi" "is_valid" "10000")
openapi-validate: (flame "openapi" "validate" "1000")
openapi-iter-errors: (flame "openapi" "iter_errors" "1000")
openapi-apply: (flame "openapi" "apply" "1000")

swagger-build: (flame "swagger" "build" "5000")
swagger-is-valid: (flame "swagger" "is_valid" "5000")
swagger-validate: (flame "swagger" "validate" "500")
swagger-iter-errors: (flame "swagger" "iter_errors" "500")
swagger-apply: (flame "swagger" "apply" "500")

geojson-build: (flame "geojson" "build" "5000")
geojson-is-valid: (flame "geojson" "is_valid" "5000")
geojson-validate: (flame "geojson" "validate" "500")
geojson-iter-errors: (flame "geojson" "iter_errors" "500")
geojson-apply: (flame "geojson" "apply" "500")

fast-valid-build: (flame "fast-valid" "build" "10000")
fast-valid-is-valid: (flame "fast-valid" "is_valid" "10000")
fast-valid-validate: (flame "fast-valid" "validate" "10000")
fast-valid-iter-errors: (flame "fast-valid" "iter_errors" "10000")
fast-valid-apply: (flame "fast-valid" "apply" "10000")

fast-invalid-build: (flame "fast-invalid" "build" "10000")
fast-invalid-is-valid: (flame "fast-invalid" "is_valid" "10000")
fast-invalid-validate: (flame "fast-invalid" "validate" "10000")
fast-invalid-iter-errors: (flame "fast-invalid" "iter_errors" "10000")
fast-invalid-apply: (flame "fast-invalid" "apply" "10000")

registry: (flame "citm" "registry" "1000")

dhat-citm-build: (dhat "citm" "build" "10000")
dhat-citm-is-valid: (dhat "citm" "is_valid" "10000")
dhat-citm-validate: (dhat "citm" "validate" "1000")
dhat-citm-iter-errors: (dhat "citm" "iter_errors" "1000")
dhat-citm-apply: (dhat "citm" "apply" "1000")

dhat-openapi-build: (dhat "openapi" "build" "10000")
dhat-openapi-is-valid: (dhat "openapi" "is_valid" "10000")
dhat-openapi-validate: (dhat "openapi" "validate" "1000")
dhat-openapi-iter-errors: (dhat "openapi" "iter_errors" "1000")
dhat-openapi-apply: (dhat "openapi" "apply" "1000")

dhat-fast-valid-build: (dhat "fast-valid" "build" "10000")
dhat-fast-valid-is-valid: (dhat "fast-valid" "is_valid" "10000")
dhat-fast-valid-validate: (dhat "fast-valid" "validate" "10000")
dhat-fast-valid-iter-errors: (dhat "fast-valid" "iter_errors" "10000")
dhat-fast-valid-apply: (dhat "fast-valid" "apply" "10000")

dhat-fast-invalid-build: (dhat "fast-invalid" "build" "10000")
dhat-fast-invalid-is-valid: (dhat "fast-invalid" "is_valid" "10000")
dhat-fast-invalid-validate: (dhat "fast-invalid" "validate" "10000")
dhat-fast-invalid-iter-errors: (dhat "fast-invalid" "iter_errors" "10000")
dhat-fast-invalid-apply: (dhat "fast-invalid" "apply" "10000")

# Clean generated flamegraphs
clean:
    rm -f *.svg perf.data perf.data.old
